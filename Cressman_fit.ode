# Cressman single neuron model

# Ornstein-Uhlenbeck
# par tau_c=3
# par sigma=0.1

# PARAMETERS

# input

par I=0

# gL, EL, gNa, gK, Vm, km, Vn, kn
par gamma1=0.044494542
par tau=100
par beta=7

par gNa=100
par gK=40
par gahp=0.01
par gkl=0.05
par gnal=0.0175
par gcil=0.05
par phi=3
par gCa=0.1
par VCa=120
par rho=1.25
par Gglia=66,66
par epsilon=1.33
par Koinf=8
par cli=6
par clO=130
par Cm=1.0

# VNa and VK change
par tauNa_1=30
par tauNa_2=40


# rates
par rK=150
par rNa=6
# par eks=0.01

# Equations


# Reversal potentials
VNa(Nai)=26.64*log(Na0(Nai)/Nai)
Vk(Ko)=26.64*log(Ko/Ki(Nai))
Vcl=26.64*log(cli/clO)



# Ion concentrations
        Na0(Nai) = 144 - beta*(Nai-18)
	 Ki(Nai) = 158 - Nai

# Gating variables
	alpham(V) = 0.1*(V+30)/(1-exp(-0.1*(V+30)))
	betam(V)=4*exp(-(V+55)/18)
	alphan(V)=0.01*(V+34)/(1-exp(-0.1*(V+34)))
	betan(V)=0.125*exp(-(V+44)/80)
	alphah(V)=0.07*exp(-(V+44)/20)
	betah(V)=1/(1+ exp(-0.1*(V+14)))
        minf(V)= alpham(V)/(alpham(V)+betam(V))

# Currents
INa(V,h)=gNa*(minf(V)*minf(V)*minf(V))*h*(V-VNa(Nai)) + gnal*(V-VNa(Nai))
IK(V,n,Cai)=(gK*(n*n*n*n)+gahp*Cai/(1+Cai))*(V-Vk(Ko))+ gkl*(V-Vk(Ko))

ICl(V)=gcil*(V-Vcl)


# Potassium
Ipump(Nai,Ko)=( rho/(1+exp((25-Nai)/3)) )*( 1/(1+exp(5.5-Ko)) )

Iglia(Ko)=Gglia/(1 + exp((18-Ko)/2.5))
Idiff(Ko)=epsilon*(Ko-Koinf)


# INTEGRATION

# Single neuron model

        # V y1
        dV/dt=1/Cm*(I-INa(V,h) -IK(V,n,Cai) -ICl(V))       
	#n y2
	dn/dt= phi*(alphan(V)*(1-n) -betan(V)*n)
	#h y3
	dh/dt= phi*(alphah(V)*(1-h) -betah(V)*h)
	#Cai y4
	dCai/dt= -0.002*gCa*(V-VCa)/(1+exp(-(V+25.)/2.5)) -Cai/80
	
	#Nai y5
	dNai/dt= 1/tau*( -gamma1* INa(V,h) - 3*Ipump(Nai,Ko) )
	#K0 y6
	dKo/dt= 1/tau*( gamma1*beta*IK(V,n,Cai) -2*beta*Ipump(Nai,Ko) -Idiff(Ko)-Iglia(Ko) )

#        wiener w
#        dnoise/dt=-noise/tau_c+w*sigma*sqrt(0.05)

# Rate approximations
# Population rate

# sigmoid fit
par ea=2.844e+04
par eb=0.1916
par ec=1.236e+04
par ksi=0.5

	dVe/dt=1/Cm*(I-gnal*(Ve-26.64*log(Na0(rNai)/rNai)) -gkl*(Ve-26.64*log(rKo/Ki(rNai))) -gcil*(Ve-Vcl))

	nuE(Ve)=(gkl+gkl+gnal)/Cm*(ea/(ec+exp(-eb*(Ve-ksi*(26.64*log(161.5/15.5)-VNa(rNai))))) )

	drNai/dt= 1/tau*(rNa*nuE(Ve) - 3*Ipump(rNai,rKo) )
	drKo/dt= 1/tau*(rK*nuE(Ve) -2*beta*Ipump(rNai,rKo) -Idiff(rKo)-Iglia(rKo) )



aux VNa=26.64*log(Na0(Nai)/Nai)

# correction
# ksi*(26.64*log(161.5/15.5)-VNa(Nai))

aux VK=26.64*log(Ko/Ki(Nai))
aux VCL=26.64*log(cli/clO)

aux nuE=nuE(Ve)*1000

# INITIAL CONDITIONS
init V = -50
init n = 0.08553
init h = 0.96859
init Cai = 0
init Nai = 15.5
init Ko = 7.8

init Ve=-50
init rNai=15.5
init rKo=7.8

# init noise=0

@ total=1000, dt=0.05, bounds=1000000000000000000000000, MAXSTOR=1000000
done
